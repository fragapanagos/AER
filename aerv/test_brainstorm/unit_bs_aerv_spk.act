// tests that bs_aerv can deliver spikes
import "aer/aerv/bs_aerv.act";
import "aer/lib/test.act";

pint M_1of4     = 3;
pint M_1of2     = 2*M_1of4;
pint p4M        = 64; // 4**M_1of4
pint M_SPK_1of4 = M_1of4+1;
pint M_SPK_1of2 = 2*M_SPK_1of4;
pint M_MEM_1of4 = M_1of4+4;
pint M_MEM_1of2 = 2*M_MEM_1of4;
pint N_SYN = p4M;
pint N_MEM = N_SYN/4;
pint D     = 4;

aer::globals g;
bool Reset = g.sReset;
aer::SINK_e1ofN<2> spk_snk[N_SYN];
aer::SINK_eMx1ofN<M_MEM_1of4-M_1of4, D> mem_snk[N_MEM];

aer::a1of2 y_syn[N_SYN];
(; n : N_SYN :
    spk_snk[n].g = g;
    spk_snk[n].d.d = y_syn[n].d;
    prs<g.Vdd, g.GND> {
        spk_snk[n].d.e => y_syn[n].a-
    }
)

aer::eMx1of4<M_MEM_1of4-M_1of4> _y_mem[N_MEM];
(; n : N_MEM :
    mem_snk[n].g = g;
    mem_snk[n].d.e = _y_mem[n].e;
)

prs<g.Vdd, g.GND> {
    (: n : N_MEM :
        (: m : M_MEM_1of4 - M_1of4 :
            (: d : D :
                _y_mem[n].m[m].d[d] => mem_snk[n].d.m[m].d[d]-
            )
        )
    )
}

aer::eMx1of2<M_SPK_1of2> spk_src;
aer::eMx1of2<M_MEM_1of2> mem_src;
(; m : M_MEM_1of2:
    (; n : 2 :
        mem_src.m[m].n[n] = g.GND;
    )
)

BS_AERV<M_1of2, M_1of4, p4M, M_SPK_1of2, M_SPK_1of4, M_MEM_1of2, M_MEM_1of4, N_SYN, N_MEM>
    aerv(g, spk_src, mem_src, y_syn, _y_mem);
