// This file contains PRS for the arbiter circuits
import "aer/lib/globals.act";
import "aer/lib/channel.act";
import "aer/lib/environment.act";

namespace aer {

namespace arbiter {
    // contains arbiter related processes. 

    export
    defproc MUTEX (globals g; bool i[2], o[2]) {
        // The basic 2-input mutual exclusion element
        bool _x[2];
    
        spec {
            mk_excllo(_x[0], _x[1])
        }
    
        [ ~LAYOUT ->
            prs <g.Vdd, g.GND> {
                //Cross-Coupled NANDs
                i[0] & _x[1] => _x[0]-
                i[1] & _x[0] => _x[1]-

                //Filter
                _x[0] => o[0]-
                _x[1] => o[1]-
            }
        [] LAYOUT ->
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                subckt {
                    //Cross-Coupled NANDs
                    i[0]<6> & _x[1]<6> -> _x[0]-
                    ~i[0]<9> | ~_x[1]<9> -> _x[0]+
                    i[1]<6> & _x[0]<6> -> _x[1]-
                    ~i[1]<9> | ~_x[0]<9> -> _x[1]+

                    //Filter
                    [keeper=0] passp<18>(_x[0], _x[1], o[0])
                    [keeper=0] _x[0]<6> -> o[0]-
    
                    [keeper=0] passp<18>(_x[1], _x[0], o[1])
                    [keeper=0] _x[1]<6> -> o[1]-
                }
            }
        ]
    }
    
    export
    defproc _MUTEX (globals g; bool _i[2], _o[2]) {
        // The basic 2-input (active low) mutual exclusion element
        bool x[2];
    
        spec {
            mk_exclhi(x[0], x[1])
        }
    
        [ ~LAYOUT ->
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                //Cross-Coupled NORs
                _i[0] | x[1] => x[0]-
                _i[1] | x[0] => x[1]-

                //Filter
                x[0] => _o[0]-
                x[1] => _o[1]-
            }
        [] LAYOUT ->
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                subckt {
                    //Cross-Coupled NORs
                    _i[0]<6> | x[1]<6> -> x[0]-
                    ~_i[0]<15> & ~x[1]<15> -> x[0]+
                    _i[1]<6> | x[0]<6> -> x[1]-
                    ~_i[1]<15> & ~x[0]<15> -> x[1]+

                    //Filter
                    [keeper=0] passn<6>(x[0], x[1], _o[0])
                    [keeper=0] ~x[0]<9> -> _o[0]+
    
                    [keeper=0] passn<6>(x[1], x[0], _o[1])
                    [keeper=0] ~x[1]<9> -> _o[1]+
                }
            }
        ]
    }
    
    namespace n_arb_2arb_node {
        // N-way arbiter using only 2 arbiters per node
        defproc CTRL (globals g; bool _ci[2], s0i, s0o, _s1i, s1o, pi, _po) {
            // sequences between children and parent communications
            bool x, y, _y, po;
            prs<g.Vdd,g.GND> {
                g._sReset & (~_ci[0] | ~_ci[1]) & ~_y -> po+
                g.pReset | _s1i & x & _y -> po-
        
                po => _po-

                g._sReset & ~x & ~_y -> s0o+
                g.pReset | x | _y -> s0o-
        
                g._sReset & ~s0i & ~x & ~y -> s1o+
                g.pReset | x -> s1o-
        
                g.pReset | ~_s1i & ~y -> x+
                g._sReset & pi & y -> x-
        
                ~pi -> y+
                s0i -> y-

                y => _y-
            }
        }
        
        defproc C_ARB (globals g; bool _si, _so, _ci, co) {
            // controlled arbiter cell for use with CTRL
            // arbitrates between child request and CTRL request
            bool cso, sso;
            bool _ci_hat, _si_hat, _co;
            _MUTEX arb(g,,);
            arb._i[0] = _ci;
            arb._i[1] = _si;
            arb._o[0] = _ci_hat;
            arb._o[1] = _si_hat;
        
            prs {
                ~_ci_hat & ~_si & ~sso -> cso+
                _ci_hat -> cso-
        
                _si & cso -> _co-
                ~_si | ~cso -> _co+
        
                ~_si_hat & ~co -> sso+
                _si_hat | co -> sso-
        
                cso | sso => _so-

                _co => co-
            }
        }

        defproc H_ARB (globals g; bool _ci[2], co[2], pi, _po) {
            // heirarchical arbiter cell
            // can be arranged in a binary tree to service more than 2 children
            CTRL ctrl(g, _ci,,,,, pi, _po);
            C_ARB c0_arb(g,,, _ci[0], co[0]), c1_arb(g,, ctrl._s1i, _ci[1], co[1]);

            prs {
                ctrl.s0o => c0_arb._si-
                c0_arb._so => ctrl.s0i- 
                ctrl.s1o => c1_arb._si-
            }
        }

        export
        template <pint N>
        defproc N_ARB_ (globals g; bool _ci[N], co[N], pi, _po) {
            // recursive utility to build an N-way arbiter tree
            [ N = 1 ->
                _ci[0] = _po;
                co[0] = pi;
            [] N = 2 ->
                H_ARB h_arb(g, _ci, co, pi, _po);
            [] N > 2 ->
                N_ARB_<N/2>   n_arb0_(g, _ci[0..N/2-1], co[0..N/2-1],,);
                N_ARB_<N-N/2> n_arb1_(g, _ci[N/2..N-1], co[N/2..N-1],,);
                H_ARB h_arb(g,,, pi, _po);
                h_arb._ci[0] = n_arb0_._po;
                h_arb._ci[1] = n_arb1_._po;
                h_arb.co[0] = n_arb0_.pi;
                h_arb.co[1] = n_arb1_.pi;
            ]
        }
    } // namespace n_arb_2arb_node

    export
    template <pint N>
    defproc N_ARB_2ARB_NODE (globals g; bool i[N], o[N]) {
        // N-way arbiter
        [ N = 2 ->
            MUTEX arb(g, i, o);
        [] N = 1 | N > 2 ->
            bool _i[N];
            n_arb_2arb_node::N_ARB_<N> n_arb_(g, _i, o,,);
            prs {
                // connect top of tree back to itself
                n_arb_._po => n_arb_.pi-
                // connect child ports to arbiter leaves
                (: n : N : i[n] => _i[n]-)
            }
        ]
    }

    export
    template <pint N>
    defproc _N_ARB_2ARB_NODE (globals g; bool _i[N], _o[N]) {
        // active low version of N_ARB
        [ N = 2 ->
            _MUTEX _arb(g, _i, _o);
        [] N = 1 | N > 2 ->
            bool o[N];
            n_arb_2arb_node::N_ARB_<N> n_arb_(g, _i, o,,);
            prs {
                // connect top of tree back to itself
                n_arb_._po => n_arb_.pi-
                // connect child ports to arbiter leaves
                (: n : N : o[n] => _o[n]-)
            }
        ]
    }


    namespace n_arb_c {
        // cheap N-way arbiter
        export
        template <pint N>
        defproc N_ARB_ (globals g; bool i[N], o[N], pi, po) {
            // utility for N_ARB_C
            // N neurons
            [ N = 1 ->
                i[0] = po;
                o[0] = pi;
            [] N > 1 ->
                bool ao[2], _ao[2], _po;
                N_ARB_<N/2> n_arb0(g, i[0..N/2-1], o[0..N/2-1], ao[0],);
                N_ARB_<N-N/2> n_arb1(g, i[N/2..N-1], o[N/2..N-1], ao[1],);
                MUTEX arb(g,,);
                arb.i[0] = n_arb0.po;
                arb.i[1] = n_arb1.po;
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                   arb.o[0] & _ao[1] | arb.o[1] & _ao[0] -> _po-
                   g.pReset | ~arb.o[0] & ~_ao[0] | ~arb.o[1] & ~_ao[1] -> _po+

                   pi & arb.o[0] & _ao[1] -> _ao[0]-
                   ~pi -> _ao[0]+

                   pi & arb.o[1] & _ao[0] -> _ao[1]-
                   ~pi -> _ao[1]+

                   _po => po-
                   _ao[0] => ao[0]-
                   _ao[1] => ao[1]-
                }
            ]
        }
        
        export
        template <pint N>
        defproc _N_ARB_ (globals g; bool _i[N], _o[N], _pi, _po) {
            // utility for _N_ARB_C
            // N neurons
            [ N = 1 ->
                _i[0] = _po;
                _o[0] = _pi;
            [] N > 1 ->
                bool _ao[2], ao[2], po;
                _N_ARB_<N/2> _n_arb0(g, _i[0..N/2-1], _o[0..N/2-1], _ao[0],);
                _N_ARB_<N-N/2> _n_arb1(g, _i[N/2..N-1], _o[N/2..N-1], _ao[1],);
                _MUTEX _arb(g,,);
                _arb._i[0] = _n_arb0._po;
                _arb._i[1] = _n_arb1._po;
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                   ~_arb._o[0] & ~ao[1] | ~_arb._o[1] & ~ao[0] -> po+
                   g.pReset | _arb._o[0] & ao[0] | _arb._o[1] & ao[1] -> po-

                   ~_pi & ~_arb._o[0] & ~ao[1] -> ao[0]+
                   _pi -> ao[0]-

                   ~_pi & ~_arb._o[1] & ~ao[0] -> ao[1]+
                   _pi -> ao[1]-

                   po => _po-
                   ao[0] => _ao[0]-
                   ao[1] => _ao[1]-
                }
            ]
        }
    } // namespace n_arb_c

    export
    template <pint N>
    defproc N_ARB_C (globals g; bool i[N], o[N]) {
        // cheap N-way arbiter
        [ N = 1 ->
            i[0] = o[0];
        [] N > 1 ->
            n_arb_c::N_ARB_<N/2> n_arb0(g, i[0..N/2-1], o[0..N/2-1],,);
            n_arb_c::N_ARB_<N-N/2> n_arb1(g, i[N/2..N-1], o[N/2..N-1],,);
            MUTEX arb(g,,);
            arb.i[0] = n_arb0.po;
            arb.i[1] = n_arb1.po;
            arb.o[0] = n_arb0.pi;
            arb.o[1] = n_arb1.pi;
        ]
    }

    export
    template <pint N>
    defproc _N_ARB_C (globals g; bool _i[N], _o[N]) {
        // cheap active-low N-way arbiter
        [ N = 1 ->
            _i[0] = _o[0];
        [] N > 1 ->
            n_arb_c::_N_ARB_<N/2> _n_arb0(g, _i[0..N/2-1], _o[0..N/2-1],,);
            n_arb_c::_N_ARB_<N-N/2> _n_arb1(g, _i[N/2..N-1], _o[N/2..N-1],,);
            _MUTEX _arb(g,,);
            _arb._i[0] = _n_arb0._po;
            _arb._i[1] = _n_arb1._po;
            _arb._o[0] = _n_arb0._pi;
            _arb._o[1] = _n_arb1._pi;
        ]
    }

    namespace n_arb_s {
        // standard, unpipelined n-way arbiter
        defproc MU_EX (globals g; bool _ci[2], co[2], qi[2], qo[2]) {
            // grants mutual exclusion between two requesting signals
            _MUTEX _arb(g, _ci,);
            qi = co;
            [ ~LAYOUT ->
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    ~co[1] & ~_arb._o[0] => qo[0]+
                    ~co[0] & ~_arb._o[1] => qo[1]+
                }
            [] LAYOUT ->
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    ~co[1]<9> & ~_arb._o[0]<9> -> qo[0]+
                    ~co[0]<9> & ~_arb._o[1]<9> -> qo[1]+
                    co[1]<6> | _arb._o[0]<6> -> qo[0]-
                    co[0]<6> | _arb._o[1]<6> -> qo[1]-
                }
            ]
        }

        defproc PREQ (globals g; bool qi[2], qo[2], pi, _po) {
            // merges mutually exclusive requests into a parent request
            bool _qo[2];
            [ ~LAYOUT ->
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    qi[0] | qi[1] => _po-

                    (: n : 2 :
                        pi & qi[n] -> _qo[n]-
                        ~pi -> _qo[n]+

                        _qo[n] -> qo[n]-
                        ~_qo[n] -> qo[n]+
                    )
                }
            [] LAYOUT ->
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    qi[0]<6> | qi[1]<6> -> _po-
                    ~qi[0]<12> & ~qi[1]<12> -> _po+

                    (: n : 2 :
                        qi[n]<6> & pi<6> -> _qo[n]-
                        ~pi<10> -> _qo[n]+

                        _qo[n]<6> -> qo[n]-
                        ~_qo[n]<9> -> qo[n]+
                    )
                }
            ]
        }

        export
        defproc NODE (globals g; bool _i[2], o[2], pi, _po) {
            // NODE in the N_ARB_S tree
            MU_EX mu_ex(g, _i, o,,);
            PREQ preq(g, mu_ex.qo, mu_ex.qi, pi, _po);
        }
    } // namespace n_arb_s

    export
    template <pint N>
    defproc N_ARB_S (globals g; bool _i[N], o[N]) {
        // Standard N-way arbiter as developed in Rajit's notes
        [ N = 1 ->
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                _i[0] => o[0]-
            }
        [] N > 1 ->
            N = (N/2)*2; // assert N power of 2
            pint N_NODE = N-1;

            _MUTEX _mutex(g,,);

            [ N_NODE = 1 ->
                _mutex._i = _i;
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    (: n : 2 :
                        ~_mutex._o[n]<9> -> o[n]+
                        _mutex._o[n]<6> -> o[n]-
                    )
                }
            [] N_NODE > 1 ->
                n_arb_s::NODE node[1..N_NODE-1]; // node[0] is bare mutex
                (; n : 1..N_NODE-1 :
                    node[n].g = g;
                )
                _mutex._i[0] = node[1]._po;
                _mutex._i[1] = node[2]._po;
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    (: n : 2 :
                        _mutex._o[n]<6> -> node[n+1].pi-
                        ~_mutex._o[n]<9> -> node[n+1].pi+
                    )
                }

                pint LEAF_BASE_IDX = N/2-1;
                (; np : 1..LEAF_BASE_IDX-1 :
                    (; nc : 2 :
                        node[np]._i[nc] = node[2*np+nc+1]._po;
                        node[np].o[nc] = node[2*np+nc+1].pi;
                    )
                )

                (; np : LEAF_BASE_IDX..N_NODE-1 :
                    (; nc : 2 :
                        node[np]._i[nc] = _i[2*(np-LEAF_BASE_IDX)+nc];
                        node[np].o[nc] = o[2*(np-LEAF_BASE_IDX)+nc];
                    )
                )
            ]
        ]
    }

    export
    template <pint N>
    defproc N_ARB_S_PREQ (globals g; bool i[N], o[N], pi, po) {
        // Standard N-way arbiter with exposed parent request port
        [ N = 1 ->
            i[0] = o[0];
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {} // HACK so netgen -B doesn't complain
        [] N > 1 ->
            N = (N/2)*2; // assert N power of 2
            pint N_NODE = N-1;

            n_arb_s::NODE node[N_NODE]; // node[0] is bare mutex
            (; n : N_NODE :
                node[n].g = g;
            )

            node[0].pi = pi;
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                node[0]._po<6> -> po-
                ~node[0]._po<12> -> po+
            }

            pint LEAF_BASE_IDX = N/2-1;
            (; np : LEAF_BASE_IDX :
                (; nc : 2 :
                    node[np]._i[nc] = node[2*np+nc+1]._po;
                    node[np].o[nc] = node[2*np+nc+1].pi;
                )
            )

            
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                (: np : LEAF_BASE_IDX..N_NODE-1 :
                    (: nc : 2 :
                        i[2*(np-LEAF_BASE_IDX)+nc]<6> -> node[np]._i[nc]-
                        ~i[2*(np-LEAF_BASE_IDX)+nc]<9> -> node[np]._i[nc]+
                    )
                )
            }
            (; np : LEAF_BASE_IDX..N_NODE-1 :
                (; nc : 2 :
                    node[np].o[nc] = o[2*(np-LEAF_BASE_IDX)+nc];
                )
            )
        ]
    }

    namespace n_arb_tree_ring {
        template <pint K>
        defproc PRODUCER_CTRL (globals g; e1of1 p, c[K]; a1of1 s) {
            bool x, _sa, _pe;
            prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                _pe & (|:k:K: c[k].d) -> x-
                ~g._pReset | ~_sa -> x+

                ~x | ~_sa => p.d+

                ~x & ~_pe => s.d+

                p.e => _pe-
                s.a => _sa-
            }
        }

        template <pint K>
        defproc SEQ_RING (globals g; a1of1 sp, sc[K]) {
            sp.d = sc[0].d;
            sc[K-1].a = sp.a;
            (; k : K-1 : sc[k].a = sc[k+1].d;)
        }

        template <pint K>
        defproc PRODUCER (globals g; e1of1 p, c[K]; a1of1 s[K]) {
            PRODUCER_CTRL<K> ctrl(g, p, c,);
            SEQ_RING<K> seq(g, ctrl.s, s);
        }

        export
        defproc CONSUMER (globals g; a1of1 s; e1of1 c) {
            MUTEX arb(g,,);
            arb.i[0] = c.d;
            arb.i[1] = s.d;
            bool c_ = arb.o[0];
            bool s_ = arb.o[1];
            bool _sta, _sfa;

            prs <g.Vdd, g.GND> {
                c_ & s.d -> _sta-
                ~c_ -> _sta+

                s_ -> _sfa-
                ~s_ -> _sfa+

                _sta | s.d -> c.e-
                ~_sta & ~s.d -> c.e+

                ~_sta | ~_sfa -> s.a+
                _sta & _sfa -> s.a-
            }
        }

        export
        template <pint M, K, N>
        defproc N_ARB_ (globals g; e1of1 p, c[N]) {
            // utility for N_ARB_TREE_RING
            [ M = 0 ->
                N = 1;
                c[0].d = p.d;
                prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
                    p.e => c[0].e-
                }
            [] M >= 1 ->
                PRODUCER<K> prod(g, p,,);
                CONSUMER cons[K];
                N_ARB_<M-1, K, N/K> _[K];
                (; k : K :
                    _[k].g = g;
                    _[k].c = c[k*N/K..(k+1)*N/K-1];
                    cons[k].g = g;
                    cons[k].c = _[k].p;
                    cons[k].s = prod.s[k];
                    prod.c[k] = _[k].p;
                )
            ]
        }
    } // namespace n_arb_tree_ring

    export
    template <pint M, K, N>
    defproc N_ARB_TREE_RING (globals g; e1of1 c[N]) {
        n_arb_tree_ring::N_ARB_<M, K, N> _(g,, c);
        _.p.d = _.p.e;
    }
} // end namespace arbiter

} // namespace aer
