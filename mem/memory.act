// contains a modified version of the brainstorm config memory definition
// assumes aer repository positioned as submodule within brainstorm repository
// i.e. as brainstorm/act/aer
import "aer/lib/channel.act";
import "aer/lib/comm.act";
import "lib/channel.act";
import "mem/interface.act";
import "mem/decoder.act";
import "mem/bank.act";
import "mem/config_bank.act";

namespace aer {

template <pint M>
defproc flip_word_order (globals g; eMx1of4<M> x, y) {
    //flips the word order from x to y
    prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {} // HACK to appease netgen -b
    x.e = y.e;
    (; m : M :
        x.m[m] = y.m[M-1-m];
    )
}

template <pint M_1of4, M_1of2>
defproc word_1of4_to_1of2 (globals g; c1of4 _x[M_1of4]; c1of2 y[M_1of2]) {
    // converts word from Mx1of4 to Mx1of2
    M_1of2 = (M_1of4*2); // assert
    prs<g.Vdd, g.GND> {
        (: m4 : M_1of4 :
            ~_x[m4].d[0]<9> | ~_x[m4].d[2]<9> -> y[m4*2].d[0]+
            _x[m4].d[0]<12> & _x[m4].d[2]<12> -> y[m4*2].d[0]-

            ~_x[m4].d[1]<9> | ~_x[m4].d[3]<9> -> y[m4*2].d[1]+
            _x[m4].d[1]<12> & _x[m4].d[3]<12> -> y[m4*2].d[1]-

            ~_x[m4].d[0]<9> | ~_x[m4].d[1]<9> -> y[m4*2+1].d[0]+
            _x[m4].d[0]<12> & _x[m4].d[1]<12> -> y[m4*2+1].d[0]-

            ~_x[m4].d[2]<9> | ~_x[m4].d[3]<9> -> y[m4*2+1].d[1]+
            _x[m4].d[2]<12> & _x[m4].d[3]<12> -> y[m4*2+1].d[1]-
        )
    }
}

template <pint words_1of4, words_1of2>
defproc addr_vn (globals g; c1of4 _addr4[words_1of4]; c1of2 _addr2[words_1of2]; bool vn) {
    // checks validity/neutrality of address data
    words_1of4 = 2; // assert
    words_1of2 = 2; // assert
    pint M4 = words_1of4;
    pint M2 = words_1of2;
    pint D4 = 4;
    pint D2 = 2;
    bool v4[M4];
    bool v2[M2];
    bool _vn4;
    bool _vn2;
    prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
        (: m : M4 :
            (& : d : D4 : _addr4[m].d[d]<12>) -> v4[m]-
            (| : d : D4 : ~_addr4[m].d[d]<9>) -> v4[m]+
        )
        (: m : M2 :
            (& : d : D2 : _addr2[m].d[d]<12>) -> v2[m]-
            (| : d : D2 : ~_addr2[m].d[d]<9>) -> v2[m]+
        )

        (& : m : M4 : ~v4[m]<12>) -> _vn4+
        (& : m : M4 : v4[m]<6>) -> _vn4-

        (& : m : M2 : ~v2[m]<12>) -> _vn2+
        (& : m : M2 : v2[m]<6>) -> _vn2-

        ~_vn4<12> & ~_vn2<12> -> vn+
        _vn4<6> & _vn2<6> -> vn-
    }
}


template <pint addr_1of4, rows, cols>
defproc config_sram_addr_decoder (globals g; c1of4 _addr[addr_1of4]; bool row_select[rows], col_select[cols], data_e) {
    // Converts addr_1of4 address into arrays of row and column select lines
    // high order word _addr[0] low order word _addr[3]
    // Only supports hardcoded addr_1of4, rows, cols 
    // Can be made to support variable parameter values if necessary
    addr_1of4 = 3; // assert
    rows = 8; // assert
    cols = 8; // assert
    pint D2 = 2;
    pint D4 = 4;

    // convert middle word to 1of2 representation
    c1of2 addr_mid[2];
    c1of2 _addr_mid[2];
    word_1of4_to_1of2<1, 2> decode_mid(g, {_addr[1]}, addr_mid);

    prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
        (: m : 2 :
            (: d : 2 :
                ~addr_mid[m].d[d] -> _addr_mid[m].d[d]+
                addr_mid[m].d[d] -> _addr_mid[m].d[d]-
            )
        )

        // we assume sufficient latency in the receiver for the bitcell to be written
        (: d : D4 :
            (: dd : D2 :
                [after=0] ~_addr[2].d[d] & ~_addr_mid[1].d[dd] -> col_select[2*d+dd]+
                [after=0] _addr[2].d[d] | _addr_mid[1].d[dd] -> col_select[2*d+dd]-
            )
        )

        // we assume sufficient latency in the receiver for the bitcell to be written
        (: d : D4 :
            (: dd : D2 :
                [after=0] ~_addr[0].d[d] & ~_addr_mid[0].d[dd] -> row_select[4*dd+d]+
                [after=0] _addr[0].d[d] | _addr_mid[0].d[dd] -> row_select[4*dd+d]-
            )
        )
    }

    c1of4 vn__addr4[2];
    c1of2 vn__addr2[2];
    vn__addr4 = {_addr[0], _addr[2]};
    vn__addr2 = {_addr_mid[0], _addr_mid[1]};
    addr_vn<2, 2> vn(g, vn__addr4, vn__addr2,); 

    prs<g.Vdd, g.GND | g.vpsub, g.vnsub> {
        ~vn.vn -> data_e+
        vn.vn -> data_e-
    }
}

export
template <pint aerv_words, word_size, cols, rows, addr_1of4>
defproc config_sram_interface (globals g; eMx1of4<aerv_words> _aerv_data; dualrail sram_write[word_size]; bool sram_col_select[cols], sram_row_select[rows]) {
    // ------------------------------
    //           aerv_data
    //  write      row/col addr
    // ------------------------------
    //   [0]  |  [     1,2,3    ]    
    // ------------------------------ 
    //    |              |       
    //    v              v      
    // 1in4to2     .--decode--.
    //    |        |          |     
    //    v        v          v
    // ------------------------------
    // write   col_select row_select
    //            sram
    // ------------------------------
    aerv_words = 4; // assert
    word_size = 2; // assert
    cols = 8; // assert
    rows = 8; //assert
    addr_1of4 = 3; // assert
    word_1of4_to_1of2<1, 2> word_4to2(g,,); // convert the write data to dual rail
    config_sram_addr_decoder<addr_1of4, rows, cols> addr_decoder(g,,,,);

    _aerv_data.e = addr_decoder.data_e;
    word_4to2._x = {_aerv_data.m[3]}; // want to raise write lines high first
    _aerv_data.m[0..2] = addr_decoder._addr;
    (; w : word_size :
        word_4to2.y[w].d = sram_write[w].d;
    )

    sram_col_select = addr_decoder.col_select;
    sram_row_select = addr_decoder.row_select;
}

} // namespace aer

namespace mem {

/*
                                  col_select
                          C0      C1      C2     C3  
                          |       |       |       |
   write =================&=======&=======&=======& 
                         | |     | |     | |     | |  
                     ____v v_____v_v_____v_v_____v_v__ 
                    | W0[f,t] W1[f,t] W2[t,f] W3[t,f] |
           r R0---->|S0                               |
           o        |                                 |
           w        |                                 |
           | R1---->|S1                               |
           s    .   |                                 |
           e    .   |              bank               |
           l    .   |                                 |
           e R14--->|S14                              |
           c        |                                 |
           t        |                                 |
             R15--->|S15                              |
                    |_________________________________|

Notes:

*/
export template <pint rows, cols, word_size, write_delay, precharge_delay, pn, width>
defproc config_sramNxM_addr_decoded(globals g; dualrail write[word_size]; bool col_select[cols], row_select[rows]; dualrail _q[rows][cols][word_size])
{
    config_arrayNxM<rows, cols, word_size> array(g.Vdd, g.GND, g.sReset, g.pReset, g._sReset, g._pReset,,,);

	/****************** write, col_select => array._bl **********************/
	dualrail _write[cols][word_size];
	dualrail __write[cols][word_size];
    // we assume sufficient latency in the receiver for the bitcell to be written
	(;i:cols:
		(;j:word_size:
			prs <g.Vdd, g.GND | g.vpsub, g.vnsub>
			{
				[after=0] write[j].f<2*width> & col_select[i]<2*width> -> _write[i][j].f-
				[after=0] ~write[j].f<2*pn*width> | ~col_select[i]<2*pn*width> -> _write[i][j].f+
				[after=0] write[j].t<2*width> & col_select[i]<2*width> -> _write[i][j].t-
				[after=0] ~write[j].t<2*pn*width> | ~col_select[i]<2*pn*width> -> _write[i][j].t+

                [after=0] _write[i][j].f<width> -> __write[i][j].f-
                [after=0] ~_write[i][j].f<pn*width> -> __write[i][j].f+
                [after=0] _write[i][j].t<width> -> __write[i][j].t-
                [after=0] ~_write[i][j].t<pn*width> -> __write[i][j].t+

				[after=0] __write[i][j].f<width> -> array._bl[i][j][0]-
				[after=0] ~__write[i][j].f<pn*width> -> array._bl[i][j][0]+
				[after=0] __write[i][j].t<width> -> array._bl[i][j][1]-
				[after=0] ~__write[i][j].t<pn*width> -> array._bl[i][j][1]+
			}
		)
	)

	/************************ row_select => array.wl *************************/
    row_select = array.wl;

	/**************************** _q => array._q *****************************/
	(;i:rows:
		(;j:cols:
			(;k:word_size:
				_q[i][j][k].d = array._q[i][j][k];
			)
		)
	)
}

} // mem
