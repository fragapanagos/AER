import "aer/lib/globals.act";
import "aer/lib/channel.act";

namespace aer {

export
template <pint N>
defproc SOURCE (globals g; bool ye, yp; c1of<N> y) {
    bool u, _yp;
    (; n : N :
        spec {
            mk_exclhi(_yp, y.d[n])
        }
    )
    (; n : N :
        (; nn : N :
            spec {
                mk_exclhi(y.d[n], y.d[nn])
            }
        )
    )

    prs<g.Vdd, g.GND> {
        ~u -> _yp-
        u & ye -> _yp+

        ~g.sReset & ~_yp -> yp+
        g.sReset | _yp -> yp-

        yp & ye -> u+
        g.pReset | ~yp & ~ye -> u-

        (: n : N :
            u & ye -> y.d[n]+
            ~u | ~ye -> y.d[n]-
        )

    }
}

export
template <pint N>
defproc BUFFER (globals g; bool xp, xe, ye, yp; c1of<N> x, y) {
    // no cycle slack buffer
    prs<g.Vdd, g.GND> {
        xp => yp+
        ye => xe+
        (: n : N :
            x.d[n] => y.d[n]+
        )
    }
}

export
template <pint N>
defproc BUFFER2 (globals g; bool xp, xe; c1of<N> x; bool ye, yp; c1of<N> y) {
    // half cycle slack buffer
    prs<g.Vdd, g.GND> {
                   xp & ~ye -> yp+
        g.pReset | ~xp & ye -> yp-

        yp & (& : n : N : ~y.d[n]) => xe+

        (: n : N :
              x.d[n] & ye -> y.d[n]+
            ~x.d[n] & ~ye -> y.d[n]-
        )
    }
}

export
template <pint N>
defproc SINK (globals g; bool xp, xe; c1of<N> x) {
    prs<g.Vdd, g.GND> {
        xp & (& : n : N : ~x.d[n]) => xe+
    }
}

} // namespace aer
