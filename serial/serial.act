import "aer/lib/globals.act";
import "aer/lib/channel.act";

namespace aer {

export
template <pint N>
defproc SOURCE (globals g; bool ye, yp; c1of<N> y) {
    bool u, _yp;
    (; n : N :
        spec {
            mk_exclhi(_yp, y.d[n])
        }
    )
    (; n : N :
        (; nn : N :
            spec {
                mk_exclhi(y.d[n], y.d[nn])
            }
        )
    )

    prs<g.Vdd, g.GND> {
        ~u -> _yp-
        u & ye -> _yp+

        ~g.sReset & ~_yp -> yp+
        g.sReset | _yp -> yp-

        yp & ye -> u+
        g.pReset | ~yp & ~ye -> u-

        (: n : N :
            u & ye -> y.d[n]+
            ~u | ~ye -> y.d[n]-
        )

    }
}

export
template <pint N>
defproc BUFFER (globals g; bool xp, xe, ye, yp; c1of<N> x, y) {
    prs<g.Vdd, g.GND> {
        xp => yp+
        ye => xe+
        (: n : N :
            x.d[n] => y.d[n]+
        )
    }
}

export
template <pint N>
defproc BUFFER2 (globals g; bool xp, xe; c1of<N> x; bool ye, yp; c1of<N> y) {
    bool _xp, _xe, __xe;
    c1of<N> _y, __y;
    __xe = xe;
    __y = y;
    prs<g.Vdd, g.GND> {
        xp => _xp-
                 ~_xp & ~ye -> yp+
        g.pReset | _xp & ye -> yp-

        yp & (& : n : N : _y.d[n]) => _xe-
        _xe => __xe-

        (: n : N :
              x.d[n] & ye -> _y.d[n]-
            ~x.d[n] & ~ye -> _y.d[n]+

            _y.d[n] => __y.d[n]-
        )
    }
}

export
template <pint N>
defproc SINK (globals g; bool xp, xe; c1of<N> x) {
    c1of<N> _x;
    bool _xe;
    prs<g.Vdd, g.GND> {
        (: n : N :
            x.d[n] => _x.d[n]-
        )

        xp & (& : n : N : _x.d[n]) => _xe-

        _xe => xe-
    }
}

} // namespace aer
